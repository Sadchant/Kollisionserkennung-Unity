#pragma kernel main

#include "0_ComputeShaderGlobals.cginc"


#define MAXCOLLIDINGOBJECTS 10

RWStructuredBuffer<uint> counterTrees : register(u0); // Input, allerdings wird dieser Shader benutzt, 
RWStructuredBuffer<uint> globalCounterTree : register(u1); // nur von Stelle 1 lesen, da steht der MaxPoint der Szene!

//cbuffer ObjectCount : register(b0)
//{
    int objectCount;
//};
//cbuffer TreeSizeInLevel : register(b1)
//{
    int4 treeSizeInLevel[SUBDIVS + 1]; // uint4, da in Constant Buffers ein Array-Eintrag immer 16 Byte hat, lese also nur von x! 
    // (könnte man auch geschickter lösen, aber an der Stelle lieber dass bisschen Speicher verschwenden als zusätzliche Instruktionen
    // zum uin4 auseinanderbauen auszuführen)
//};

[numthreads(LINEAR_XTHREADS, LINEAR_YTHREADS, LINEAR_ZTHREADS)]
void main(uint3 DTid : SV_DispatchThreadID)
{
    uint id = DTid.x;
    // obligatorische Überprüfung für den Block, der "zu wenig" zu tun hat
    if (id >= treeSizeInLevel[SUBDIVS].x)
        return;
    if (id == 0) // sollte die id 0 sein ist es die Zelle in der die gesamte Szene liegt,
    {
        globalCounterTree[id] = 4294967295; //trage den MaximalWert von uint ein, damit die Zelle nicht weniger Überprüfungen als ihre 8 Kinder hat
        return;
    }
    uint objectsInThisCellCount = 0; // wie viele Objekte besitzen Dreiecke, die in der von diesem Thread bearbeiteten Zelle liegen?
    uint boundingBoxCounts[MAXCOLLIDINGOBJECTS]; // pro Objekt die Anzahl der Dreiecke, ohne 0er, hieraus wird der letztendliche intersectionTestCount berechnet

    // gehe über alle Countertrees (1 pro Objekt)
    for (uint i = 0; i < objectCount; i++)
    {
        // berechne die Id der Zelle in Countertrees, die in diesem Durchlauf verrechnet wird
        uint curID = id + i * treeSizeInLevel[SUBDIVS].x;
        // hole die Anzahl der Dreiecke in dieser Zelle
        uint curBoundingBoxCount = counterTrees[curID];
        // sollte es Dreiecke in dieser Zelle geben, merke dir die Anzahl in boundingBoxCounts
        // erhöhe außerdem objectsInThisCellCount um 1
        if (curBoundingBoxCount > 0)
            boundingBoxCounts[objectsInThisCellCount++] = curBoundingBoxCount;
        // setze die bearbeitete Zelle für den nächsten Durchgang von FillCounterTrees wieder auf 0
        counterTrees[curID] = 0;
    }
    // berechne nun aus der Dreiecks-Anzahl von jedem Objekt die insgesamt benötigten Überschneidungstests
    uint intersectionTestCount = 0;
    // laufe über alle Dreiecks-Anzahl-Werte
    for (uint j = 0; j < objectsInThisCellCount; j++)
    {
        // erhöhe intersectionTestCount mit den Zahlen für alle Dreiecks-Anzahl-Werte, die nach dem jten Wert in objectsInThisCellCount stehen
        for (uint k = j + 1; k < objectsInThisCellCount; k++)
        {
            intersectionTestCount += boundingBoxCounts[j] * boundingBoxCounts[k];
        }
    }
    globalCounterTree[id] = intersectionTestCount; // speichere das Ergebnis im Output-Array
}